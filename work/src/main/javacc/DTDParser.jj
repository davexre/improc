/**
 * https://javacc.org/contrib/DTDParser.jj
 *
 * name:            DTDParser.jj
 * modified:        Apr 2, 2001
 * author:          John Gebbie
 * email:           j_gebbie@yahoo.com
 */

options {
	DEBUG_PARSER = false;
}

PARSER_BEGIN(DTDParser)

package com.slavi.tools.dtd.parser;

import java.io.InputStream;
import java.io.FileInputStream;
import java.io.FileNotFoundException;
import java.util.HashMap;
import java.util.ArrayList;

public class DTDParser
{
	public HashMap<String, String> entities = new HashMap();

	private String content = null;

	public static void parse(InputStream aInSt) throws ParseException {
		// create a parser (this object)
		DTDParser parser = new DTDParser(aInSt);
		// parse!
		parser.dtd();
	}

	public static void main(String[] args) throws ParseException, FileNotFoundException {
		parse(new FileInputStream(args[0]));
	}
}

PARSER_END(DTDParser)

TOKEN :
{
	<PERCENT:	"%"								>
|	<LETTER:	[ "A"-"Z", "a"-"z" ]			>
|	<DIGIT:		[ "0"-"9" ]						>
|	<NAMECHAR:	( <LETTER> | <DIGIT> | "." | "-" | "_" | ":" )		>
|	<FIXED:		"#FIXED"						>
|	<IMPLIED:	"#IMPLIED"						>
|	<PCDATA:	"#PCDATA"						>
|	<REQUIRED:	"#REQUIRED"						>
|	<CDATA:		"CDATA"							>
|	<ANY:		"ANY"							>
|	<EMPTY:		"EMPTY"							>
|	<ID:		"ID"							>
|	<IDREF:		"IDREF"							>
|	<IDREFS:	"IDREFS"						>
|	<ENTITY:	"ENTITY"						>
|	<ENTITIES:	"ENTITIES"						>
|	<NMTOKEN:	"NMTOKEN"						>
|	<NMTOKENS:	"NMTOKENS"						>
|	<NOTATION:	"NOTATION"						>
|	<NAME:		( <LETTER> | "_" | ":" | <PERCENT> ) ( <NAMECHAR> | ";" )*		>
|	<QUOTEDSTR:	( "'" (~["'"])* "'" ) | (  "\"" (~["\""])* "\"")	>
}

SKIP :
{
	<	( " " | "\t" | "\n" | "\r" )+					>
		// comment decl updated by Gagandeep Bhatia [mailto:gagan@ebprovider.com]
|	<	"<!--" ( ~["-"] | ( "-" ~["-"] ) )* "-->"			>
|	<	"<?"   (~[">"])* ">"						>
}

void dtd() :
{
}
{
	( elementDecl() | attListDecl() | entityDecl() )* <EOF>
}

void elementDecl() :
{
}
{
	"<!ELEMENT"		{ content = null; }
	<NAME>			{ String name = token.image; }
	(
		<EMPTY>
		| <ANY> 	{ content = "ANY"; }
		| LOOKAHEAD(2) mixed() | children()
	)	
	">"
}

void mixed() :
{
}
{
	"(" <PCDATA> ( ( "|" <NAME> )* ")*" | ")" )
}

void children() :
{
}
{
	seqchoice() [ modifier() ]
}

void seqchoice() :
{
}
{
	"(" cp() ( choice() | seq() ) ")"
}

void choice() :
{
}
{
	( "|" cp() )+
}

void seq() :
{
}
{
	( "," cp() )*
}

void cp() :
{
	String name = null;
	String modifier = "";
}
{
	(
	(
		<NAME>				{ name = token.image; }
		| seqchoice()
	) 
	[ "*" | "+" | "?" ]		{ modifier = token.image; }
	) {
		
	}
}

void modifier() :
{
}
{
	"*" | "+" | "?"
}

void attListDecl() :
{
}
{
	"<!ATTLIST" <NAME> ( attribDef() )* ">"
}

void attribDef() :
{
}
{
	<NAME> attribType() defaultDecl()
}

void attribType() :
{
}
{
	<CDATA> | tokenizedType() | enumeratedType()
}

void tokenizedType() :
{
}
{
	<ID> | <IDREF> | <IDREFS> | <ENTITY> | <ENTITIES> | <NMTOKEN> | <NMTOKENS>
}

void enumeratedType() :
{
}
{
	( notationType() | enumeration() )
}

void notationType() :
{
}
{
	<NOTATION> "(" <NAME> ( "|" <NAME> )* ")"
}

void enumeration() :
{
}
{
	"(" <NAME> ( "|" <NAME> )* ")"
}

void defaultDecl() :
{
}
{
	<REQUIRED> | <IMPLIED> | ( [ <FIXED> ] <QUOTEDSTR> )
}

void entityDecl() :
{
}
{
	"<!ENTITY" (<PERCENT>)? 
	<NAME>						{ String name = token.image;			}
	<QUOTEDSTR>					{ entities.put(name, token.image);		}
	">"
}
